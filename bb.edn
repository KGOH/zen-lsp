{:paths ["script"]
 :deps  {borkdude/gh-release-artifact
         {:git/url "https://github.com/borkdude/gh-release-artifact"
          :sha     "f34f3e382e6a0ef7f52748b2f27eb681f799a822"}}
 :tasks {:requires          ([babashka.fs :as fs])
         :init              (do (defn print-public-task [k]
                                  (let [{:keys [:private :name]} (current-task)]
                                    (when-not private
                                      (println (case k :enter "☐" "✓") name))))
                                (def version "0.0.2-SNAPSHOT"))
         :leave             (print-public-task :leave)

         clean              {:doc      "Cleans artifacts"
                             :task     (fs/delete-tree "server/target")}

         java1.8            {:doc      "Asserts that we are using java 1.8"
                             :requires ([clojure.java.shell :refer [sh]]
                                        [clojure.string :as str])
                             :task     (let [java-version (:err (sh "java" "-version"))]
                                         (assert (str/includes? java-version "1.8.0") "Must use Java 1.8.0"))}

         server:test        {:doc "Run tests"
                             :task (clojure {:dir "server"} "-M:test")}

         uberjar            {:task    (str (fs/path "target" (str "zen-lsp-server-" version "-standalone.jar")))}

         server:build       {:doc     "Produces lsp server standalone jar"
                             :depends [java1.8 uberjar]
                             :task  (let [jar (fs/file "server" uberjar)]
                                      (when (seq (fs/modified-since jar
                                                                    ["server/deps.edn" "server/src"]))
                                        (clojure {:dir "server"} "-T:build uber" ":uber-file" (pr-str uberjar)))
                                      (str jar))}

         vscode-server      {:doc     "Copies lsp server jar to vscode extension"
                             :depends [uberjar server:build]
                             :task    (let [jar (fs/path "vscode-extension" "zen-lsp-server-standalone.jar")]
                                        (fs/copy (fs/file "server" uberjar)
                                                 jar
                                                 {:replace-existing true})
                                        (str jar))}

         vscode-version     {:doc     "Prepares package.json with up to date zen-lsp version"
                             :task    (binding [*command-line-args* [version]]
                                        (load-file "script/update-package-json.clj"))}

         vscode-package     {:doc    "Builds vscode package"
                             :depends [vscode-server vscode-version]
                             :task    (do (shell {:dir "vscode-extension"} "vsce package")
                                          (str (fs/file "vscode-extension" (format "zen-lsp-%.vsix"))))}

         upload-assets      {:doc    "Uploads jar and vsix to Github"
                             :depends [server:build vscode-server vscode-version vscode-package]
                             :requires ([upload-release :as ur])
                             :task    (do (ur/release server:build version)
                                          (ur/release vscode-package version))}

         vscode-publish     {:doc     "Publishes vscode extension to marketplace"
                             :depends [vscode-server vscode-version]
                             :task    (shell {:dir "vscode-extension"} "vsce publish")}

         ovsx-publish       {:doc     "Publishes vscode extension to ovsx thing"
                             :depends [vscode-server vscode-version]
                             :task    (shell {:dir "vscode-extension"}
                                             (str "ovsx publish --pat "
                                                  (System/getenv "OVSX_TOKEN")))}

         publish            {:doc     "The mother of all tasks: publishes everything needed for new release"
                             :depends [upload-assets #_#_ vscode-publish ovsx-publish]}}}
